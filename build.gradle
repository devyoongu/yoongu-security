plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.8'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'maven-publish'
}

apply plugin: 'java-library'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven-publish'

group = 'com.spring.security'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }        // for Query DSL
    maven { url "https://repo.spring.io/snapshot" }         // for Query DSL
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'org.modelmapper:modelmapper:2.3.8'
    implementation 'org.apache.commons:commons-lang3:3.0'
    implementation 'com.google.guava:guava:28.0-jre'
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.4'

    compileOnly 'org.projectlombok:lombok'
    testImplementation 'com.mysql:mysql-connector-j'

    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    // QueryDSL
    implementation 'com.querydsl:querydsl-core'
    implementation 'com.querydsl:querydsl-jpa'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa" // querydsl JPAAnnotationProcessor 사용 지정
    annotationProcessor "jakarta.persistence:jakarta.persistence-api" // java.lang.NoClassDefFoundError(javax.annotation.Entity) 발생 대응
    annotationProcessor "jakarta.annotation:jakarta.annotation-api" // java.lang.NoClassDefFoundError (javax.annotation.Generated) 발생 대응
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation "com.querydsl:querydsl-jpa"
    testAnnotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    def targetVersion = project.findProperty('targetVersion') ?: '1.0.0'

    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/devyoongu/yoongu-security")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USER_NAME")
                password = project.findProperty("gpr.key") ?: System.getenv("ACCESS_TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            groupId 'com.yoongu.security'
            artifactId 'yoongu-security'
            version targetVersion
            from(components.java)
        }
    }
}


bootJar {
    enabled = false
}

jar {
    enabled = true
}
